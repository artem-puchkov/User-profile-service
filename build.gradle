plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.0'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'checkstyle'
    id 'maven-publish'
}

group = 'com.iprody.user.profile'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

subprojects {

    group 'com.iprody.user.profile'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '17'

    apply from: "$rootProject.projectDir/version.gradle"

    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'checkstyle'
    apply plugin: 'maven-publish'
    apply plugin: 'java'

    publishing {
        publications {
            github(MavenPublication) {
                artifactId = 'user-profile-service'
                from components.java
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
            }
        }

        repositories {
            maven {
                name = "GitHub"
                url = uri("https://maven.pkg.github.com/iPrody/user-profile-gr00003")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }

    repositories {
        mavenCentral()
        maven {
            name = "GitHub"
            url = uri("https://maven.pkg.github.com/iPrody/user-profile-gr00003")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }

    ext {
        isProd = project.hasProperty('env') ? project.property('env') == 'prod' : false
    }

    processResources {
        if (isProd) {
            exclude '**/db.changelog-e2e.yml'
            exclude '**/add-test-users.yml'
        }
        doLast {
            println "Gradle Profile Enabled: ['prod' => $isProd']"
        }
    }

    processTestResources {
        if (isProd) {
            from 'src/main/resources'
            include '**/db.changelog-e2e.yml'
            include '**/add-test-users.yml'
        }
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}